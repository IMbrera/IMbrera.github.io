## Обработка запроса 1с.
<!---include(header.html)--->
<br>
<div class="row">
<div class="col-md-3"></div>
<div class="col-md-6">

<!---RESULT_ADD--->

</div>
<div class="col-md-3"></div>
</div>

<!---include(footer.html)--->


Переменные.Вставить("TITLE", ЯзыковыеДанные.CREATE_TICKET);

Попытка

	УстановитьПривилегированныйРежим(Истина);

	Тема = СтрЗаменить(ЗначениеPOST(POST, "topic"), Символы.ПС, "");
	СервисСтрокой = СтрЗаменить(ЗначениеPOST(POST, "service"), Символы.ПС, "");
	УслугаСтрокой = СтрЗаменить(ЗначениеPOST(POST, "usluga"), Символы.ПС, "");
	КлиентСтрокой = СтрЗаменить(ЗначениеPOST(POST,"username"),Символы.ПС, "");
	Если НЕ ПустаяСтрока(Тема) Тогда
	
		// Смотрим, есть ли уже такой документ созданный в течении последней минуты и если есть не создаем.
		Запрос = Новый Запрос();
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Задание.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.Задание КАК Задание
			|ГДЕ
			|	Задание.Тема = &Тема
			|	И Задание.Автор = &Автор
			|	И Задание.Дата МЕЖДУ &ДатаНач И &ДатаКон
			|
			|УПОРЯДОЧИТЬ ПО
			|	Задание.МоментВремени УБЫВ";
		Запрос.УстановитьПараметр("ДатаНач", ТекущаяДатаСеанса() - 60);
		Запрос.УстановитьПараметр("ДатаКон", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("Тема", Тема);
		Запрос.УстановитьПараметр("Автор", АвторизированныйПользователь); 
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда

			НовоеЗадание 				= Документы.Задание.СоздатьДокумент();
			ТекущееЗадание  			= Документы.Задание.ПолучитьСсылку(Новый УникальныйИдентификатор());			
			НовоеЗадание.УстановитьСсылкуНового(ТекущееЗадание);
			
			// Файлы
			Попытка
				КоличествоФайлов = Число(ЗначениеPOST(POST, "files_counter"));
			Исключение
				КоличествоФайлов = 0;
			КонецПопытки;
			
			// Добавляем файлы.
			Для Индекс = 1 По КоличествоФайлов Цикл
				
				ИмяФайла = ИмяФайлаPOST(POST, "FILE_" + Формат(Индекс, "ЧРД=; ЧРГ=; ЧН=0; ЧГ="));
				Если ПустаяСтрока(ИмяФайла) Тогда
					Продолжить;
				КонецЕсли;

				Данные = ЗначениеPOST(POST, "FILE_" + Формат(Индекс, "ЧРД=; ЧРГ=; ЧН=0; ЧГ=")); 
				Если Данные = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				Если СтрНайти(ИмяФайла, ".") = 0 Тогда
					РасширениеБезТочки = "";
					ИмяБезРасширения = ИмяФайла;
				Иначе
					РасширениеБезТочки = Сред(ИмяФайла, СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца) + 1);
					ИмяБезРасширения = Лев(ИмяФайла, СтрНайти(ИмяФайла, ".", НаправлениеПоиска.СКонца) - 1);
				КонецЕсли;

				Структура = Новый Структура;
				Структура.Вставить("Автор", 						АвторизированныйПользователь);
				Структура.Вставить("ВладелецФайлов", 				ТекущееЗадание);
				Структура.Вставить("ИмяБезРасширения", 				ИмяБезРасширения);
				Структура.Вставить("РасширениеБезТочки", 			РасширениеБезТочки);
				Структура.Вставить("ВремяИзмененияУниверсальное", 	ТекущаяУниверсальнаяДата());
				Адрес = ПоместитьВоВременноеХранилище(Данные);
				РаботаСФайлами.ДобавитьФайл(Структура, Адрес);
				УдалитьИзВременногоХранилища(Адрес);

			КонецЦикла;

			// Задание
			НовоеЗадание.Дата			= ТекущаяДатаСеанса();
			НовоеЗадание.СпособСоздания	= Справочники.СпособыСозданияЗаданий.ЛичныйКабинет;
			НовоеЗадание.Тема 			= Тема;
			НовоеЗадание.Срочность		= Перечисления.Срочность.Средняя;
			НовоеЗадание.Влияние		= Перечисления.Влияние.Среднее;
			НовоеЗадание.Процесс		= Константы.ОсновнойПроцесс.Получить();
			НовоеЗадание.ТипПроцесса	= НовоеЗадание.Процесс.ТипПроцесса;
			НовоеЗадание.ТекстHTML 		= "<html><body>" + ЗначениеPOST(POST, "editor") + "</body></html>";
			// НовоеЗадание.Инициатор		= АвторизированныйПользователь;
			НовоеЗадание.ДатаСоздания 	= НовоеЗадание.Дата;
			НовоеЗадание.Автор 		 	= АвторизированныйПользователь;
			НовоеЗадание.ТекущийЭтап 	= Справочники.ЭтапыПроцессов.Новый;
			//	НовоеЗадание.Клиент = Справочники.Сотрудники.НайтиПоНаименованию(Клиент);					
			// Сервис
			Сервис = Справочники.Сервисы.ПолучитьСсылку(Новый УникальныйИдентификатор(СервисСтрокой));
			Если ЗначениеЗаполнено(Сервис) Тогда
				НовоеЗадание.Сервис 	= Сервис;
			КонецЕсли;

			// Услуга
			Услуга = Справочники.Услуги.ПолучитьСсылку(Новый УникальныйИдентификатор(УслугаСтрокой));
			Если ЗначениеЗаполнено(Услуга) Тогда
				НовоеЗадание.Услуга 	= Услуга;
			КонецЕсли;
			
        	// Клиент
        	Клиент = Справочники.Сотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор(КлиентСтрокой));
			Если ЗначениеЗаполнено(Клиент) Тогда
				НовоеЗадание.Клиент 	= Клиент;
			КонецЕсли;
			
			// Инициатор
			Инициатор = Справочники.Сотрудники.ПолучитьСсылку(Новый УникальныйИдентификатор(КлиентСтрокой));
			Если ЗначениеЗаполнено(Клиент) Тогда
				НовоеЗадание.Инициатор 	= Клиент;
			КонецЕсли;
 	                  	
			НовоеЗадание.Записать(РежимЗаписиДокумента.Проведение);
			
			Если НЕ НовоеЗадание.Ссылка.Пустая() Тогда
				РегистрыСведений.РеквизитыЗаданийПоПользователям.УстановитьДатуОткрытия(НовоеЗадание.Ссылка);
			КонецЕсли;
			
		КонецЕсли;	

		Переменные.Вставить("RESULT_ADD", "<div class='alert alert-success alert-dismissible'><h4><i class='icon fa fa-check'></i> <!---SUCCESS---></h4> <!---SUCCESS_ALL---></div>");
		
	Иначе
		Переменные.Вставить("RESULT_ADD", "<div class='alert alert-error alert-dismissible'><h4><i class='icon fa fa-exclamation'></i> <!---ERROR---></h4> <!---TASK_CREATED---></div>");
	КонецЕсли;
	
Исключение

	Переменные.Вставить("RESULT_ADD", "<div class='alert alert-error alert-dismissible'><h4><i class='icon fa fa-exclamation'></i> <!---ERROR---></h4> " + ОбработатьТеги(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())) + "</div>");
	
КонецПопытки;